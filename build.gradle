import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        maven {
            url "https://jcenter.bintray.com"
        }
        jcenter()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    // minecraft 'net.minecraftforge:forge:1.15.1-30.0.41'
    minecraft 'net.minecraftforge:forge:1.15.2-31.1.47'
}

final def date = new Date()
final def dailyVersion = (new SimpleDateFormat("yyyy-MM-dd-hh-mm-ss")).format(date)

version = dailyVersion
group = "rikka.librikka" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "LibRikka"

// Need this here so eclipse task generates correctly.
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    // mappings channel: 'snapshot', version: '20190719-1.14.3'
    mappings channel: 'snapshot', version: '20200225-1.15.1'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                librikka {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                librikka {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', 'librikka', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                librikka {
                    source sourceSets.main
                }
            }
        }
    }
}

// configure the source folders 
sourceSets {
    main {
        java {
            srcDir "src/main/java"
        }
        resources {
            srcDir "src/main/resources"
        }
    }
}

jar.version = version
jar {
    manifest {
        attributes([
                "Specification-Title": "librikka",
                "Specification-Vendor": "CISM",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor" :"CISM",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

def libsDir = new File(System.getenv("LIBS_DIR") ?: "build/libs/")
task deobfJar(type: Jar) {
    destinationDir = libsDir
    from sourceSets.main.output
    manifest = jar.manifest
    classifier = 'dev'
}

task sourceJar(type: Jar) {
    destinationDir = libsDir
    from "src/main/java"
    from "src/main/resources"
    classifier "sources"
}

task copyFiles(type: Copy) {
    from 'src/fakemod/classes/'
    into "build/classes/java/main"
    rename { filename ->
        filename.replace 'javac', 'class'
    }
}

tasks.classes.dependsOn('copyFiles')

artifacts {
    archives deobfJar
    archives sourceJar
}


bintray {
    user = System.getenv('bintray-user')
    key = System.getenv('bintray-key')
    publications = ['JarPublication', 'SrcPublication']
    pkg {
        repo = 'librikka'
        name = 'librikka'
        userOrg = 'manageryzy'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/rikka0w0/librikka'
        version {
            name = 'librikka-' + dailyVersion
            desc = 'daily build'
            released = new Date()
        }
    }
}



publishing {
    publications {
        JarPublication(MavenPublication) {
            from components.java

            artifact deobfJar {
            }
        }

        SrcPublication(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }

        }
    }

    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
